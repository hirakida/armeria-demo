plugins {
    id 'org.jruyi.thrift' version '0.4.1' apply false
    id 'org.springframework.boot' version '2.1.4.RELEASE' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.jruyi.thrift'

    group = 'com.example'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = JavaVersion.VERSION_11

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom 'com.linecorp.armeria:armeria-bom:0.83.0'
        }
    }

    dependencies {
        annotationProcessor('org.projectlombok:lombok')
        compileOnly('org.projectlombok:lombok')
        implementation('com.linecorp.armeria:armeria-thrift')
        implementation('org.apache.thrift:libthrift:0.12.0')
    }

    compileThrift {
        sourceDir "${rootProject.rootDir}/thrift"
    }
}

project(':thrift-server') {
    dependencies {
        implementation('com.linecorp.armeria:armeria-spring-boot-webflux-starter')
        runtimeOnly('org.springframework.boot:spring-boot-devtools')
        testImplementation('org.springframework.boot:spring-boot-starter-test')
    }
}

project(':thrift-client') {
    dependencies {
        implementation('org.springframework.boot:spring-boot-starter')
        testImplementation('org.springframework.boot:spring-boot-starter-test')
    }
}
