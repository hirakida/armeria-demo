plugins {
    id 'org.springframework.boot' apply false
    id 'org.jetbrains.kotlin.jvm' apply false
    id 'com.google.protobuf' version '0.9.3' apply false
    id 'org.jruyi.thrift' version '0.4.2' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.example'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = JavaVersion.VERSION_17

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        dependencies {
            dependency 'org.apache.thrift:libthrift:0.17.0'
            dependency 'io.grpc:protoc-gen-grpc-java:1.53.0'
            dependency 'org.projectlombok:lombok:1.18.28'
            dependency ("org.springframework.boot:spring-boot-dependencies:$springBootVersion")

            dependencySet(group: 'com.squareup.retrofit2', version: '2.9.0') {
                entry 'converter-jackson'
                entry 'adapter-rxjava3'
            }
            dependencySet(group: 'com.google.protobuf', version: '3.22.0') {
                entry 'protobuf-java'
                entry 'protoc'
            }
        }
    }

    dependencies {
        implementation platform('com.linecorp.armeria:armeria-bom:1.23.1')
        implementation 'org.jetbrains:annotations:24.0.1'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}
