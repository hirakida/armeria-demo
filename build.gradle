plugins {
    id 'org.springframework.boot' version "$springBootVersion" apply false
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion" apply false
    id 'org.jetbrains.kotlin.plugin.spring' version "$kotlinVersion" apply false
    id 'com.google.protobuf' version '0.9.4' apply false
    id 'com.linecorp.thrift-gradle-plugin' version '0.5.0' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.example'
    version = '0.0.1-SNAPSHOT'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
    }

    compileJava {
        options.deprecation = true
        options.compilerArgs += '-parameters'
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        dependencies {
            dependency ("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
            dependency 'org.apache.thrift:libthrift:0.17.0'
            dependency 'io.grpc:protoc-gen-grpc-java:1.60.0'
            dependency 'javax.annotation:javax.annotation-api:1.3.2'

            dependencySet(group: 'com.squareup.retrofit2', version: '2.9.0') {
                entry 'converter-jackson'
                entry 'adapter-rxjava3'
            }
            dependencySet(group: 'com.google.protobuf', version: '3.25.1') {
                entry 'protobuf-java'
                entry 'protoc'
            }
        }
    }

    dependencies {
        implementation platform('com.linecorp.armeria:armeria-bom:1.26.4')
        implementation platform('io.netty:netty-bom:4.1.100.Final')
        implementation 'org.jetbrains:annotations:24.0.1'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}
