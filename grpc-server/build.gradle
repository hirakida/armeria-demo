plugins {
    id 'java'
    id 'idea'
    id 'com.google.protobuf' version '0.8.8'
    id 'org.springframework.boot' version '2.1.3.RELEASE'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 11

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'com.linecorp.armeria:armeria-bom:0.81.0'
    }
}

dependencies {
    implementation('com.linecorp.armeria:armeria-spring-boot-webflux-starter')
    implementation('com.linecorp.armeria:armeria-grpc')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
}

protobuf {
    def protobufVersion = '3.6.1'
    def grpcVersion = '1.18.0'
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        ofSourceSet('main')*.plugins {
            grpc {}
        }
        all().each { task ->
            task.generateDescriptorSet = true
            task.descriptorSetOptions.includeSourceInfo = true
            task.descriptorSetOptions.includeImports = true
            task.descriptorSetOptions.path =
                    "${buildDir}/resources/main/META-INF/armeria/grpc/grpc-demo.dsc"
        }
    }
}
